#!/usr/bin/perl -w
###APPNAME:     discuz_download
###APPAUTHOR:   duel
###APPDATE:	2009-01-26 07:54:08
###APPVER:	0.1
###APPDESC:     discuz_download	
###APPUSAGE:	
###APPEXAMPLE:	discuz_download
###APPOPTION:	
use strict;

#ENV variable MUST be defined somewhere,
#FOR perl to search modules from,
#OR nothing will work
use lib $ENV{XR_PERL_MODULE_DIR};

use MyPlace::Script::Usage qw/help_required help_even_empty/;
exit 0 if(help_required($0,@ARGV));
#exit 0 if(help_even_empty($0,@ARGV));

use MyPlace::Discuz;
use MyPlace::ReEnterable;
use MyPlace::Script::Message;
use Cwd qw/getcwd/;
use constant {
    RESUME_DATA_FILE=>".discuz_download.resume",
    THREAD_DBASE_FILE=>".threads",
};
use utf8;

binmode STDERR,"utf8";

my %THREAD_DBASE;
my $CWD = getcwd();
my $HND = MyPlace::ReEnterable->new('main');
my ($url,$TYPE,$USER,$PASS) = @ARGV;
$TYPE ||= "text";

my $AUTORENAME=0;

sub build_dir {
    my $f =shift;
    my $s = shift;
    #$s =~ s/^(\[[^\[\]]+\]\s*)+//g;
    $s =~ s/\s*[\/\\\|]\s*/-/g;
    $s =~ s/【[^【】]+\s*阅\s*】//;
    $s =~ s/【\s*作者(?:：|:)不(?:祥|详)\s*】\s*//;
    $s =~ s/^\s+//;
    $s =~ s/\s+$//;
    return "$f/$s";
}
sub loadpage {
    my ($url,$USER,$PASS) = @_;
    app_message "loading $url...";
    sleep 2;
    my $page = MyPlace::Discuz->new(user=>$USER,pass=>$PASS);
    if($page->init_with_url($url)) {
        app_message "--no-prefix","\t[OK]\n";
        return $page;
    }
    else {
        app_warning "--no-prefix","\t[Failed]\n";
        return undef;
    }
}

sub save_post {
    my ($dir,$filename,$url,$prompt) = @_;
    $prompt = $prompt ? "[$prompt]" : "";
    my $subpage = loadpage($url,$USER,$PASS);
#    app_message "$prompt $url ...";
    unless($subpage and $subpage->{post}) {
        app_warning "Empty post...\n";
        return undef;
    }
    app_message "Saving...";
    mkdir $dir unless(-d $dir);
    if($TYPE eq "images") {
        save_images($dir,$filename,$subpage);
    }
    elsif($TYPE eq "all") {
        save_images($dir,$filename,$subpage);
        save_text($dir,$filename,$subpage);
    }
    else {
        save_text($dir,$filename,$subpage);
    }
    &log_item($url);
}

sub log_item {
    my $str = shift;
    return undef if($THREAD_DBASE{$str});
    $str =~ s/^[a-zA-Z]+:\/+[^\/]+//;
    return undef if($THREAD_DBASE{$str});
    $THREAD_DBASE{$str}=1;
    return 1;
}


sub save_images {
    my $dir = shift;
    my $filename = shift;
    my $subpage = shift;
#    my $prompt = shift;
    my @images = $subpage->get_post_images($subpage->{post});
    unless(@images) {
        app_warning "--no-prefix","\tNO IMAGES\n";
        return undef;
    }
    my $dst = build_dir($dir,$filename);
    mkdir $dst unless(-d $dst);
    foreach my $idx (0 .. $#images) {  
        app_message "\n[" . ($idx+1)  . "/$#images]" if(@images > 1);
        app_message "--no-prefix","$images[$idx]\n-->$dst/$idx.jpg ...\n";
        if(-f "$dst/$idx.jpg") {
            app_message "\t[Skipped] File exists\n";
            next;
        }
        print "\n";
        open FI,"-|","netcat",$images[$idx];
        my @data = <FI>;
        unless(@data) {
            close FI;
            next;
        }
        unless(open FO,">","$dst/$idx.jpg") {
            app_error "Error:$!\n";
            close FI;
            next;
        }
        print FO @data;
        close FO;
        close FI;
    }
}

sub get_filename {
    my ($fst,$sec)=@_;
    $sec ="" unless($sec);
    my $idx=0;
    while (-e "$fst$sec") {
        $idx++;
        $fst="$fst\_$idx";
    }
    return "$fst$sec";
}

sub save_text {
    my $dir = shift;
    my $filename = shift;
    my $subpage = shift;
#    my $prompt = shift;
    if($AUTORENAME) {
        $filename = &autorename($filename);
    }
    my $dst = build_dir($dir,$filename);
    $dst = &get_filename($dst,".txt");
    app_message "==>$dst ...";
    mkdir $dir unless(-d $dir);
    return (app_error "--no-prefix","\t[Failed:]$dir not exists!\n") unless(-d $dir);
    if(-f $dst) { 
        app_warning "--no-prefix","\t[Skipped] File exists\n";
        return undef;
    }
    my $text;
    if($subpage) {
        $text = $subpage->get_post_text($subpage->{post});
    }
    else {
        $text = shift;
    }
    unless($text) {
        app_message "--no-prefix","\tNo text to save [Failed]\n";
        return undef;
    }
    open FO,">:utf8",$dst or return (app_message "--no-prefix","\t[Failed:]$!\n");
    print FO @{$text};
    close FO;
    app_message "--no-prefix","\t[Done]\n";
    return 1;
}


sub min_max {
    return (shift() + 1) . "/" .  (shift() + 1)
}

sub process_url {
    my $dir = shift;
    my $url = shift;
    my $no_subpage = shift;
    my $prompt = shift;
    my $page = loadpage($url,$USER,$PASS);
    mkdir $dir unless(-d $dir);
    return unless($page);
    if($page->{post}) {
        save_post($dir,$page->{title} ,undef,$prompt,$page->get_post_text($page->{post}));
        $page->delete();
        return 1;
    }
    if(!$no_subpage and $dir eq ".") {
        my $title = $page->{title};$title =~ s/\s*-.*//g;
        $dir = build_dir($dir,$title);
        mkdir $dir unless(-d $dir);
    }
    if($page->{forums}) {
        foreach my $idx (0 .. $#{$page->{forums}}) {
            my $subdir = build_dir($dir,$page->{forums}->[$idx]->[1]);
            my $suburl = $page->build_url($page->{forums}->[$idx]->[0],$url);
            $HND->push(undef,"process_url", $subdir,$suburl,0,$prompt . " forum " . min_max($idx,$#{$page->{forums}}));
        }
    }
    if((!$no_subpage) and  $page->{pages}) {
            #use Data::Dumper;die(Dumper($page->{pages}),"\n");
        foreach my $idx (0 .. $#{$page->{pages}}) {
            my $suburl = $page->build_url($page->{pages}->[$idx],$url);
            $HND->push(undef,"process_url", $dir,$suburl,1,$prompt . " page " . min_max($idx,$#{$page->{pages}}));
        }
    }
    else {
        foreach my $idx (0 .. $#{$page->{threads}}) {
            my $suburl = $page->build_url($page->{threads}->[$idx]->[0],$url);
            if(!&log_item($suburl)) {
                app_message "$suburl:\n\t in THREAD_DBASE, ignored it\n";
                next;
            }
            my $subfile = $page->{threads}->[$idx]->[1];
            $HND->push(undef,"save_post",$dir,$subfile,$suburl,$prompt . " thread " . min_max( $idx,$#{$page->{threads}} . " "));
        }
    }
    $page->delete();
}

my $killingme=0;
sub sig_killed {
    return if($killingme);
    $killingme=1;
    app_warning "I AM KILLED!!!\n";
    if($HND->{lastStack}) {
        $HND->push(@{$HND->{lastStack}});
    }
    chdir($CWD);
    app_message "saving remained tasks...\n";
    $HND->saveToFile(RESUME_DATA_FILE);
    app_message "saving thread dbase...\n";
    open FO,">",THREAD_DBASE_FILE;
    foreach(keys %THREAD_DBASE) {
        print FO "$_\n";
    }
    close FO;
    app_message $HND->length," tasks saved to ",RESUME_DATA_FILE,"\n";
    exit 2;
}
$SIG{INT}=\&sig_killed;

my @urls;
if($url) {
    if($url eq '-') {
        app_message "Input URLs:\n";
        while(<STDIN>) {
            chomp;
            push @urls,$_;
        }
        app_message scalar(@urls)," URLs read.\n";
    }
    else {
        push @urls,$url;
    }
}

if(@urls) {
    $url = $urls[0];
    $HND->setState("type",$TYPE);
    $HND->setState("user",$USER);
    $HND->setState("urls",[@urls]);
    $HND->setState("pass",$PASS);
    $HND->push(undef,"process_url",".",$_,0,"") foreach(@urls);
    $HND->saveToFile(RESUME_DATA_FILE);
}
else {
    app_message "Loading resuming data...";
    $HND->loadFromFile(RESUME_DATA_FILE);
    $TYPE = $HND->getState("type") || "text";
    $USER = $HND->getState("user"); 
    $PASS = $HND->getState("pass"); 
    app_message "--no-prefix","\tGet " . $HND->length . " tasks\n";
}

unless($USER) {
    app_message "Input user name,please:";
    $USER = readline(*STDIN);
    chomp $USER;
    $HND->setState("user",$USER);
}
unless($PASS) {
    app_message "Input password,please:";
    $PASS = readline(*STDIN);
    chomp $PASS;
    $HND->setState("pass",$PASS);
}

die("Usage:$0 (URL) [text/images] [username] [password]\n") unless($HND->length);

if(-f THREAD_DBASE_FILE) {
    open FI,"<",THREAD_DBASE_FILE;
    while(<FI>) {
        chomp;
        $THREAD_DBASE{$_}=1;
    }
    close FI;
}

if( -f "autorename.pl") {
    $AUTORENAME=1;
    do "autorename.pl";
    #import "autorename.pl";
}


while(my $tasks = $HND->length) {
    app_message "[$tasks] tasks remained\n";
    $HND->run();
}
chdir($CWD);
unlink RESUME_DATA_FILE if(-f RESUME_DATA_FILE);
app_message "saving thread dbase...\n";
open FO,">",THREAD_DBASE_FILE;
foreach(keys %THREAD_DBASE) {
    print FO "$_\n";
}
close FO;





